apply plugin: 'com.android.application'

android {
    def keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(rootProject.file("keystore.properties")))
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "edu.stanford.thingengine.engine"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 152
        versionName '1.5.2'
        manifestPlaceholders = [HOCKEYAPP_APP_ID: "d6e09574c0f9434e8e20e8d8f2234285"]

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++14"
            }
        }
        ndk {
            abiFilters 'x86'//,'armeabi-v7a', 'x86_64', 'arm64-v8a' //select ABIs to build APKs for
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            jniDebuggable true
        }
    }

    def execTask = project.tasks.create("buildJs", Exec)
    execTask.commandLine "./build_js.sh", "./src/main/assets/jxcore"

    applicationVariants.all { variant ->
        variant.mergeResources.dependsOn(execTask)
        variant.mergeAssets.dependsOn(execTask)
    }
    productFlavors {
    }
    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:25.1.1'
    compile 'com.google.android.gms:play-services-location:10.0.1'
    compile 'com.google.android.gms:play-services-maps:10.0.1'
    compile 'com.google.android.gms:play-services-places:10.0.1'
    compile 'com.android.support:support-v13:25.1.1'
    compile 'com.android.support:support-v4:25.1.1'
    compile 'com.android.support:recyclerview-v7:25.1.1'
    compile 'com.android.support:percent:25.1.1'
    compile 'com.microsoft.projectoxford:speechrecognition:1.2.0'
    compile 'com.koushikdutta.ion:ion:2.+'
    compile 'com.google.android:flexbox:0.2.3'
    compile 'net.hockeyapp.android:HockeySDK:4.0.1'
}
