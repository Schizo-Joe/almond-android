apply plugin: 'com.android.application'

android {
    def keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(rootProject.file("keystore.properties")))
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "edu.stanford.thingengine.engine"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 160
        versionName '1.6.0'
        manifestPlaceholders = [HOCKEYAPP_APP_ID: "d6e09574c0f9434e8e20e8d8f2234285"]

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++14"
                arguments "-DANDROID_STL=c++_shared", "-DANDROID_CPP_FEATURES=exceptions"
            }
        }
        ndk {
            abiFilters 'x86','armeabi-v7a'//, 'x86_64', 'arm64-v8a' //select ABIs to build APKs for
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            jniDebuggable true
        }
    }

    def execTask = project.tasks.create("buildJs", Exec)
    execTask.commandLine "./build_js.sh", "./src/main/assets/"

    applicationVariants.all { variant ->
        variant.mergeResources.dependsOn(execTask)
        variant.mergeAssets.dependsOn(execTask)
    }
    productFlavors {
    }
    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.google.android.gms:play-services-location:10.2.4'
    compile 'com.google.android.gms:play-services-maps:10.2.4'
    compile 'com.google.android.gms:play-services-places:10.2.4'
    compile 'com.google.android.gms:play-services-auth:10.2.4'
    compile 'com.android.support:support-v13:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:percent:25.3.1'
    compile 'com.microsoft.projectoxford:speechrecognition:1.2.0'
    compile 'com.koushikdutta.ion:ion:2.1.9'
    compile 'com.google.android:flexbox:0.2.3'
    compile 'net.hockeyapp.android:HockeySDK:4.0.1'
}
